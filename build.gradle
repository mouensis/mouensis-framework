plugins {
    id 'io.spring.dependency-management' version '1.0.10.RELEASE' apply false
    id "me.champeau.gradle.jmh" version "0.5.0" apply false
}

apply plugin: 'java'

ext {
    moduleProjects = subprojects.findAll { it.name.startsWith("mouensis-") }
}

configure(moduleProjects) { project ->
    apply from: "${rootDir}/gradle/mouensis-module.gradle"
}

configure(allprojects) { project ->
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    ext {
        set('springBootVersion', "2.4.1")
        set('springDocVersion', "1.5.0")
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
        }
        repositories {
            mavenLocal()
            mavenCentral()
            maven { url "https://repo.spring.io/libs-spring-framework-build" }
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
    }

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, "seconds"
            cacheDynamicVersionsFor 0, "seconds"
        }
    }
}

/**
 * 关闭父项目的打包功能
 */
jar{
    enabled=false
}